buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:${property("staging.version")}"
        classpath "nu.studer:gradle-credentials-plugin:${property("credentials.version")}"
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.11'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'nu.studer.credentials'
apply plugin: 'io.codearte.nexus-staging'

import io.franzbecker.gradle.lombok.task.DelombokTask

group = 'com.exsoloscript.challonge'
archivesBaseName = 'challonge-java'
version = '1.0.4-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: property('retrofit2.version')
    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: property('retrofit2.gson.version')
    compile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: property('okhttp.logging')
    compile group: 'commons-codec', name: 'commons-codec', version: property('commons.codec.version')
    compile group: 'org.apache.commons', name: 'commons-lang3', version: property('commons.lang3.version')
    compile group: 'com.google.inject', name: 'guice', version: property('guice.version')
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: property('guice.version')
    // testing
    testCompile group: 'junit', name: 'junit', version: property('junit.version')
}

test {
    environment 'CHALLONGE_USERNAME', System.getenv().containsKey('CHALLONGE_USERNAME') ? System.getenv('CHALLONGE_USERNAME') : challongeUsername
    environment 'CHALLONGE_API_KEY', System.getenv().containsKey('CHALLONGE_API_KEY') ? System.getenv('CHALLONGE_API_KEY') : challongeApiKey
    filter {
        includeTestsMatching 'com.exsoloscript.challonge.ChallongeTestSuite'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

lombok {
    version = "1.16.18"
    sha256 = ""
}

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Release with: ./gradlew -Prelease uploadArchives closeAndReleaseRepository
if (project.hasProperty('release')) {
    allprojects {
        ext."signing.password" = credentials.signingPassword

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    // Snapshot repository
                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'challonge-java'
                        packaging 'jar'
                        url 'https://github.com/stefangeyer/challonge-java'
                        description 'Java binding for the CHALLONGE! REST Api'

                        scm {
                            connection 'scm:git:git://github.com/stefangeyer/challonge-java.git'
                            developerConnection 'scm:git:git@github.com/stefangeyer/challonge-java.git'
                            url 'http://github.com/stefangeyer/challonge-java/tree/master'
                        }

                        licenses {
                            license {
                                name 'Apache 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }

                        developers {
                            developer {
                                id 'stefangeyer'
                                name 'Stefan Geyer'
                            }
                        }
                    }
                }
            }
        }

        nexusStaging {
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}